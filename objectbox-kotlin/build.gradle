group = 'io.objectbox'
version= rootProject.version

buildscript {
    ext.javadocDir = "$buildDir/docs/javadoc"
    ext.kotlin_version = '1.3.31'
    ext.dokka_version = '0.10.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.7

dokka {
    outputFormat = 'html'
    outputDirectory = javadocDir

    // Fix "Can't find node by signature": have to manually point to dependencies.
    // https://github.com/Kotlin/dokka/wiki/faq#dokka-complains-about-cant-find-node-by-signature-
    configuration{
        externalDocumentationLink {
            // Point to web javadoc for objectbox-java packages.
            url = new URL("https://objectbox.io/docfiles/java/current/")
            // Note: Using JDK 9+ package-list is now called element-list.
            packageListUrl = new URL(url, "element-list")
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from "$javadocDir"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    // java plugin adds jar.
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile project(':objectbox-java')

    //testCompile 'junit:junit:4.12'
}


uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Kotlin'
                description 'ObjectBox is a fast NoSQL database for Objects'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}