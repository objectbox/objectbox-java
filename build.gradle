buildscript {
    ext {
        // Typically, only edit those two:
        def objectboxVersionNumber = '2.6.0-RC' // without "-SNAPSHOT", e.g. '2.5.0' or '2.4.0-RC'
        def objectboxVersionRelease = true  // set to true for releasing to ignore versionPostFix to avoid e.g. "-dev" versions

        // version post fix: '-<value>' or '' if not defined; e.g. used by CI to pass in branch name
        def versionPostFixValue = project.findProperty('versionPostFix')
        def versionPostFix = versionPostFixValue ? "-$versionPostFixValue" : ''
        ob_version = objectboxVersionNumber + (objectboxVersionRelease? "" : "$versionPostFix-SNAPSHOT")

        // Native library version for tests
        // Be careful to diverge here; easy to forget and hard to find JNI problems
        def nativeVersion = objectboxVersionNumber + (objectboxVersionRelease? "": "-dev-SNAPSHOT")
        def osName = System.getProperty("os.name").toLowerCase()
        def objectboxPlatform = osName.contains('linux') ? 'linux'
                : osName.contains("windows")? 'windows'
                : osName.contains("mac")? 'macos'
                : 'unsupported'
        ob_native_dep = "io.objectbox:objectbox-$objectboxPlatform:$nativeVersion"

        junit_version = '4.13'
        mockito_version = '3.3.3'
        kotlin_version = '1.3.61'
        dokka_version = '0.10.1'

        println "version=$ob_version"
        println "objectboxNativeDependency=$ob_native_dep"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.0.5"
    }
}

allprojects {
    group = 'io.objectbox'
    version = ob_version

    repositories {
        mavenCentral()
        jcenter()
    }
    
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'  // SNAPSHOTS
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

def projectNamesToPublish = [
        'objectbox-java-api',
        'objectbox-java',
        'objectbox-kotlin',
        'objectbox-rxjava',
        'objectbox-rxjava3'
]

def hasSigningProperties() {
    return (project.hasProperty('signingKeyId')
            && project.hasProperty('signingKeyFile')
            && project.hasProperty('signingPassword'))
}

configure(subprojects.findAll { projectNamesToPublish.contains(it.name) }) {
    apply plugin: 'maven'
    apply plugin: 'signing'

    configurations {
        deployerJars
    }

    dependencies {
        // Using an older version to remain compatible with Wagon API used by Gradle/Maven
        deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:3.2.0'
        deployerJars 'org.apache.maven.wagon:wagon-ftp:3.3.2'
    }

    signing {
        if (hasSigningProperties()) {
            String signingKey = new File(signingKeyFile).text
            useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
            sign configurations.archives
        } else {
            println "Signing information missing/incomplete for ${project.name}"
        }
    }

    // Use afterEvaluate or all dependencies will be lost in the generated POM
    afterEvaluate {
        uploadArchives {
            repositories {
                mavenDeployer {
                    def preferredRepo = project.findProperty('preferredRepo')
                    println "preferredRepo=$preferredRepo"
                    
                    if (preferredRepo == 'local') {
                        repository url: repositories.mavenLocal().url
                        println "Uploading archives to mavenLocal()."
                    } else if (preferredRepo != null
                            && project.hasProperty('preferredUsername') 
                            && project.hasProperty('preferredPassword')) {
                        if (!hasSigningProperties()) {
                            throw new InvalidUserDataException("To upload to repo signing is required.")
                        }

                        configuration = configurations.deployerJars

                        // replace placeholders
                        def repositoryUrl = preferredRepo
                                .replace('__groupId__', project.group)
                                .replace('__artifactId__', project.archivesBaseName)
                        repository(url: repositoryUrl) {
                            authentication(userName: preferredUsername, password: preferredPassword)
                        }

                        println "Uploading archives to $repositoryUrl."
                    } else if (project.hasProperty('sonatypeUsername')
                            && project.hasProperty('sonatypePassword')) {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                        def isSnapshot = version.endsWith('-SNAPSHOT')
                        def sonatypeRepositoryUrl = isSnapshot
                                ? "https://oss.sonatype.org/content/repositories/snapshots/"
                                : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                        repository(url: sonatypeRepositoryUrl) {
                            authentication(userName: sonatypeUsername, password: sonatypePassword)
                        }

                        println "Uploading archives to $sonatypeRepositoryUrl."
                    } else {
                        println "WARNING: preferredRepo or credentials NOT set, can not upload archives."
                    }

                    pom.project {
                        packaging 'jar'
                        url 'https://objectbox.io'

                        scm {
                            url 'https://github.com/objectbox/objectbox-java'
                            connection 'scm:git@github.com:objectbox/objectbox-java.git'
                            developerConnection 'scm:git@github.com:objectbox/objectbox-java.git'
                        }

                        developers {
                            developer {
                                id 'ObjectBox'
                                name 'ObjectBox'
                            }
                        }

                        issueManagement {
                            system 'GitHub Issues'
                            url 'https://github.com/objectbox/objectbox-java/issues'
                        }

                        organization {
                            name 'ObjectBox Ltd.'
                            url 'https://objectbox.io'
                        }
                    }
                }
            }
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
