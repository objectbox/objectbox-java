apply plugin: 'java'
apply plugin: 'findbugs'

group = 'io.objectbox'
version= rootProject.version

sourceCompatibility = '1.7'

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
    ignoreFailures = true
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':objectbox-java-api')
    compile 'org.greenrobot:essentials:3.0.0-RC1'
    compile 'com.google.flatbuffers:flatbuffers-java:1.11.1'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
}

javadoc {
    // Hide internal API from javadoc artifact.
    exclude("**/io/objectbox/Cursor.java")
    exclude("**/io/objectbox/KeyValueCursor.java")
    exclude("**/io/objectbox/ModelBuilder.java")
    exclude("**/io/objectbox/Properties.java")
    exclude("**/io/objectbox/Transaction.java")
    exclude("**/io/objectbox/model/**")
    exclude("**/io/objectbox/ideasonly/**")
    exclude("**/io/objectbox/internal/**")
    exclude("**/io/objectbox/reactive/DataPublisherUtils.java")
    exclude("**/io/objectbox/reactive/WeakDataObserver.java")
}

// Note: use packageJavadocForWeb to get as ZIP.
// Note: the style changes only work if using JDK 10+.
task javadocForWeb(type: Javadoc) {
    group = 'documentation'
    description = 'Builds Javadoc incl. objectbox-java-api classes with web tweaks.'

    def srcApi = project(':objectbox-java-api').file('src/main/java/')
    if (!srcApi.directory) throw new GradleScriptException("Not a directory: ${srcApi}", null)
    // Hide internal API from javadoc artifact.
    def filteredSources = sourceSets.main.allJava.matching {
        exclude("**/io/objectbox/Cursor.java")
        exclude("**/io/objectbox/KeyValueCursor.java")
        exclude("**/io/objectbox/ModelBuilder.java")
        exclude("**/io/objectbox/Properties.java")
        exclude("**/io/objectbox/Transaction.java")
        exclude("**/io/objectbox/model/**")
        exclude("**/io/objectbox/ideasonly/**")
        exclude("**/io/objectbox/internal/**")
        exclude("**/io/objectbox/reactive/DataPublisherUtils.java")
        exclude("**/io/objectbox/reactive/WeakDataObserver.java")
    }
    source = filteredSources + srcApi

    classpath = sourceSets.main.output + sourceSets.main.compileClasspath
    destinationDir = reporting.file("web-api-docs")

    title = "ObjectBox Java ${version} API"
    options.bottom = 'Available under the Apache License, Version 2.0 - <i>Copyright &#169; 2017-2020 <a href="http://objectbox.io/">ObjectBox Ltd</a>. All Rights Reserved.</i>'

    doLast {
        // Note: frequently check the vanilla stylesheet.css if values still match.
        def stylesheetPath = "$destinationDir/stylesheet.css"

        // Primary background
        ant.replace(file: stylesheetPath, token: "#4D7A97", value: "#17A6A6")

        // "Active" background
        ant.replace(file: stylesheetPath, token: "#F8981D", value: "#7DDC7D")

        // Hover
        ant.replace(file: stylesheetPath, token: "#bb7a2a", value: "#E61955")

        // Note: in CSS stylesheets the last added rule wins, so append to default stylesheet.
        // Code blocks
        file(stylesheetPath).append("pre {\nwhite-space: normal;\noverflow-x: auto;\n}\n")
        // Member summary tables
        file(stylesheetPath).append(".memberSummary {\noverflow: auto;\n}\n")
        // Descriptions and signatures
        file(stylesheetPath).append(".block {\n" +
                "    display:block;\n" +
                "    margin:3px 10px 2px 0px;\n" +
                "    color:#474747;\n" +
                "    overflow:auto;\n" +
                "}")
    }
}

task packageJavadocForWeb(type: Zip, dependsOn: javadocForWeb) {
    group = 'documentation'
    description = 'Packages Javadoc incl. objectbox-java-api classes with web tweaks as ZIP.'

    archiveFileName = "objectbox-java-web-api-docs.zip"
    destinationDirectory = file("$buildDir/dist")

    from reporting.file("web-api-docs")

    doLast {
        println "Javadoc for web packaged to ${file("$buildDir/dist/objectbox-java-web-api-docs.zip")}"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    // java plugin adds jar.
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Java (only)'
                description 'ObjectBox is a fast NoSQL database for Objects'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

// this task is also used by the composite build ('objectbox-deploy'), check before making changes
task verifyVersion {
    group 'verify'
    doLast {
        // verify version in Boxstore.java
        File storeFile = file('src/main/java/io/objectbox/BoxStore.java')
        def versionLine = storeFile.filterLine { line ->
            line.contains("String VERSION =")
        }.toString()

        if (versionLine == null || versionLine.empty) {
            throw new GradleException('Could not find VERSION in ObjectStore.cpp')
        }

        // matches snippet like '12.34.56'
        def boxStoreVersion = versionLine.find("\\d+\\.\\d+\\.\\d+")
        assert ob_expected_version == boxStoreVersion
    }
}
