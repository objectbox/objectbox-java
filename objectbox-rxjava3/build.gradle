buildscript {
    ext.javadocDir = "$buildDir/docs/javadoc"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Produce Java 8 byte code, would default to Java 6.
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = javadocDir

    // Fix "Can't find node by signature": have to manually point to dependencies.
    // https://github.com/Kotlin/dokka/wiki/faq#dokka-complains-about-cant-find-node-by-signature-
    configuration{
        externalDocumentationLink {
            // Point to web javadoc for objectbox-java packages.
            url = new URL("https://objectbox.io/docfiles/java/current/")
            // Note: Using JDK 9+ package-list is now called element-list.
            packageListUrl = new URL(url, "element-list")
        }
    }
}

dependencies {
    compile project(':objectbox-java')
    compile 'io.reactivex.rxjava3:rxjava:3.0.1'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "junit:junit:$junit_version"
    testCompile "org.mockito:mockito-core:$mockito_version"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from "$javadocDir"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    // java plugin adds jar.
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox RxJava 3 API'
                description 'RxJava 3 extensions for ObjectBox'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
