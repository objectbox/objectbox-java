apply plugin: 'java'

group = 'io.objectbox'
version= rootProject.version

sourceCompatibility = 1.7

dependencies {
    compile project(':objectbox-java')
    compile 'io.reactivex.rxjava2:rxjava:2.1.16'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.19.0'
}

javadoc {
    failOnError = false
    title = "ObjectBox RxJava2 ${version} API"
    excludes = [] // Unfinished APIs if any
    options.bottom = 'Available under the Apache License, Version 2.0 - <i>Copyright &#169; 2018 <a href="http://objectbox.io/">ObjectBox Ltd</a>. All Rights Reserved.</i>'
    doLast {
        copy {
            from '../javadoc-style/'
            into "build/docs/javadoc/"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox RxJava API'
                description 'RxJava extension for ObjectBox'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
